// MapCycleBuilder
// Copyright (c) 2025 Jericho Crosby (Chalwk)
// Licensed under the MIT License.

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '2.26.0'
}

application {
    mainClass = 'com.chalwk.Main'
    mainModule = 'com.chalwk'
}

group = 'com.chalwk'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

jlink {
    launcher {
        name = 'MapCycleBuilder'
    }

    options = ['--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages']

    jpackage {
        installerType = 'exe'
        installerOptions = [
                '--description', 'A JavaFX application for generating mapcycle.txt and mapvotes.txt files for game servers.',
                '--vendor', 'Jericho Crosby',
                '--win-menu',
                '--win-shortcut',
                '--win-dir-chooser',
                '--verbose',
                '--about-url', 'https://chalwk.github.io',
                '--copyright', 'Â© Copyright 2025 MapCycleBuilder | Jericho Crosby (Chalwk)',
                '--license-file', 'src/main/resources/LICENSE.txt',
        ]

        imageOptions = [
                '--icon', 'src/main/resources/icon.ico'
        ]
    }
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.1.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
            includes = ['**/*']
        }
    }
}